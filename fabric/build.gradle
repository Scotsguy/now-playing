plugins {
    id("fabric-loom") version("${loom_version}")
}

base {
    archivesName = "${mod_id}-fabric-${minecraft_version}"
}

dependencies {
    compileOnly(project(":common"))
    minecraft("com.mojang:minecraft:${minecraft_version}")
    mappings(loom.officialMojangMappings())

    compileOnly("io.github.llamalad7:mixinextras-fabric:${mixinextras_version}")
    annotationProcessor("io.github.llamalad7:mixinextras-fabric:${mixinextras_version}")

    modImplementation("net.fabricmc:fabric-loader:${fabric_loader_version}")
    modImplementation("net.fabricmc.fabric-api:fabric-api:${fabric_api_version}")

    modImplementation("maven.modrinth:modmenu:${modmenu_version}")

    modImplementation("me.shedaniel.cloth:cloth-config-fabric:${clothconfig_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
}

loom {
    if (project(":common").file("src/main/resources/${mod_id}.accesswidener").exists()) {
        accessWidenerPath = project(":common").file("src/main/resources/${mod_id}.accesswidener")
    }
    mixin {
        defaultRefmapName = "${mod_id}.refmap.json"
    }
}

tasks.withType(JavaCompile).configureEach {
    source(project(":common").sourceSets.main.allSource)
}
tasks.named("sourcesJar", Jar) {
    from(project(":common").sourceSets.main.allSource)
}
processResources {
    from project(":common").sourceSets.main.resources
}
