pluginManagement {
    repositories {
        gradlePluginPortal()
        mavenCentral()
        exclusiveContent {
            forRepository {
                maven {
                    name = 'Fabric'
                    url = uri('https://maven.fabricmc.net')
                }
            }
            filter {
                includeGroup('net.fabricmc')
                includeGroup('fabric-loom')
            }
        }
        exclusiveContent {
            forRepository {
                maven {
                    name = 'Sponge'
                    url = uri('https://repo.spongepowered.org/repository/maven-public')
                }
            }
            filter {
                includeGroupAndSubgroups("org.spongepowered")
            }
        }
    }
}

plugins {
    // https://plugins.gradle.org/plugin/org.gradle.toolchains.foojay-resolver-convention
    id "org.gradle.toolchains.foojay-resolver-convention" version "0.8.0"
}

// This should match the folder name of the project, else IDEA may complain (https://youtrack.jetbrains.com/issue/IDEA-317606)
rootProject.name = "now-playing"
include("common")
include("fabric")
include("neoforge")
